#Область Форма

#Область События

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	тз_ВсеПользователи = РеквизитФормыВЗначение("ВсеПользователи");
	
	ПользователиБазы = ПользователиИнформационнойБазы.ПолучитьПользователей();
	
	Для каждого ПользовательБазы Из ПользователиБазы Цикл
		стр_ВсеПользователи = тз_ВсеПользователи.Добавить();
		ЗаполнитьЗначенияСвойств(стр_ВсеПользователи, ПользовательБазы);
	КонецЦикла;
	
	тз_ВсеПользователи.Сортировать("Имя");
	ЗначениеВРеквизитФормы(тз_ВсеПользователи, "ВсеПользователи");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = "Навигатор" Тогда
		
		Если ИмяСобытия = "ЗакрытьВсеОкнаРедакторов" Тогда
			Попытка
				Закрыть();
			Исключение
				//
			КонецПопытки;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Элементы
#КонецОбласти

#Область Таблицы

#Область ВсеПользователи

&НаКлиенте
Процедура ВсеПользователиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	#Область Предусловия
	
	ТекущиеДанные = Элементы.ВсеПользователи.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	ПараметрыФормы = Новый Структура("ИдПользователя", Новый УникальныйИдентификатор(ТекущиеДанные.УникальныйИдентификатор));
	
	ПолноеИмяФормы = ПолноеИмяФормыОбработки(ЭтаФорма.ИмяФормы, "ПользовательБазы");
	ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, ЭтаФорма, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Команды

&НаКлиенте
Процедура КомандаЗапуститьОтИмениПользователя(Команда)
	
	#Область Предусловия
	
	ТекущиеДанные = Элементы.ВсеПользователи.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	#КонецОбласти
	
	ПараметрыФормы = Новый Структура("ИдПользователя", Новый УникальныйИдентификатор(ТекущиеДанные.УникальныйИдентификатор)); 
	
	ПолноеИмяФормы = ПолноеИмяФормыОбработки(ЭтаФорма.ИмяФормы, "ЗапускОтИмениПользователя");
	
	ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, ЭтаФорма, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Процедуры
#КонецОбласти

#Область Функции

&НаСервере
Функция ПолноеИмяФормыОбработки(ИмяТекущейФормы, ИмяФормыОбработки)
	
	Результат = ЭтаОбработка().ПолноеИмяФормыОбработки(ИмяТекущейФормы, ИмяФормыОбработки);
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЭтаОбработка()
	
	Результат = РеквизитФормыВЗначение("Объект");
	Возврат Результат;
	
КонецФункции

#КонецОбласти